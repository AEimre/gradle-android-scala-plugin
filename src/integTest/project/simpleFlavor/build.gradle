buildscript {
    repositories {
        mavenCentral()
        maven {
            url snaphotRepositoryUrl
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
        classpath "jp.leafytree.gradle:gradle-android-scala-plugin:$androidScalaPluginVersion"
    }
}

repositories {
    jcenter()
}

apply plugin: "com.android.application"
apply plugin: "android-scala"

android {
    compileSdkVersion androidPluginCompileSdkVersion
    buildToolsVersion androidPluginBuildToolsVersion

    defaultConfig {
        minSdkVersion Integer.decode(androidPluginMinSdkVersion)
        targetSdkVersion Integer.decode(androidPluginTargetSdkVersion)
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
        versionCode 1
        versionName "1.0"
    }

    productFlavors {
        flavor1 {
            applicationId "jp.leafytree.android.hello.flavor1"
        }

        flavor2 {
            applicationId "jp.leafytree.android.hello.flavor2"
        }
    }

    dexOptions {
        incremental androidPluginIncremental.toBoolean()
        preDexLibraries androidPluginPreDexLibraries.toBoolean()
        jumboMode androidPluginJumboMode.toBoolean()
    }
}

dependencies {
    compile "com.google.android:multidex:0.1"
    compile "org.scala-lang:scala-library:$scalaLibraryVersion"
}

afterEvaluate {
    tasks.matching {
        it.name.startsWith('dex')
    }.each { dx ->
        if (dx.additionalParameters == null) {
            dx.additionalParameters = []
        }
        dx.additionalParameters += '--multi-dex'
        dx.additionalParameters += "--main-dex-list=$projectDir/main-dex-list.txt".toString()
    }
}
